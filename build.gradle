plugins {
    id "com.jfrog.bintray" version "1.1"
    id "com.github.jruby-gradle.base" version "1.5.0"
    id "scala"
    id "checkstyle"
}

import com.github.jrubygradle.JRubyExec
repositories {
    mavenCentral()
    jcenter()
}
configurations {
    provided
}

version = "0.2.0"

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile "org.scala-lang:scala-library:2.11.8"

    compile  "org.embulk:embulk-core:0.8.13"
    provided "org.embulk:embulk-core:0.8.13"

    compile "com.amazonaws:aws-java-sdk-dynamodb:1.10.43"

    testCompile "junit:junit:4.+"
    testCompile "org.embulk:embulk-standards:0.8.13"
    testCompile "org.embulk:embulk-core:0.8.13:tests"
}

compileScala {
    scalaCompileOptions.metaClass.useCompileDaemon = true
}

tasks.withType(ScalaCompile) {
    configure(scalaCompileOptions.forkOptions) {
        memoryMaximumSize = '1g'
        jvmArgs = ['-XX:MaxPermSize=1024m']
    }
}

// Activating the Zinc based compiler
tasks.withType(ScalaCompile) {
    scalaCompileOptions.metaClass.useAnt = false
}

task classpath(type: Copy, dependsOn: ["jar"]) {
    doFirst { file("classpath").deleteDir() }
    from (configurations.runtime - configurations.provided + files(jar.archivePath))
    into "classpath"
}
clean { delete "classpath" }

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

task gem(type: JRubyExec, dependsOn: ["gemspec", "classpath"]) {
    jrubyArgs "-S"
    script "gem"
    scriptArgs "build", "${project.name}.gemspec"
    doLast { ant.move(file: "${project.name}-${project.version}.gem", todir: "pkg") }
}

task gemPush(type: JRubyExec, dependsOn: ["gem"]) {
    jrubyArgs "-S"
    script "gem"
    scriptArgs "push", "pkg/${project.name}-${project.version}.gem"
}

task "package"(dependsOn: ["gemspec", "classpath"]) {
    doLast {
        println "> Build succeeded."
        println "> You can run embulk with '-L ${file(".").absolutePath}' argument."
    }
}

task gemspec {
    ext.gemspecFile = file("${project.name}.gemspec")
    inputs.file "build.gradle"
    outputs.file gemspecFile
    doLast { gemspecFile.write($/
Gem::Specification.new do |spec|
  spec.name          = "${project.name}"
  spec.version       = "${project.version}"
  spec.authors       = ["Daisuke Higashi"]
  spec.summary       = %[Dynamodb input plugin for Embulk]
  spec.description   = %["Loads records from Dynamodb."]
  spec.email         = ["daisuke.develop@gmail.com"]
  spec.licenses      = ["MIT"]
  spec.homepage      = "https://github.com/lulichn/embulk-input-dynamodb"

  spec.files         = `git ls-files`.split("\n") + Dir["classpath/*.jar"]
  spec.test_files    = spec.files.grep(%r"^(test|spec)/")
  spec.require_paths = ["lib"]

  #spec.add_dependency 'YOUR_GEM_DEPENDENCY', ['~> YOUR_GEM_DEPENDENCY_VERSION']
  spec.add_development_dependency 'bundler', ['~> 1.0']
  spec.add_development_dependency 'rake', ['~> 12.0']
end
/$)
    }
}
clean { delete "${project.name}.gemspec" }

