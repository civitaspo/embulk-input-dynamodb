plugins {
    id "scala"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.5.5"
    id "com.diffplug.spotless" version "6.15.0"
    // note: We cannot use the latest version because of the following error.
    // > org/eclipse/jgit/lib/Repository has been compiled by a more recent version of the Java Runtime (class file version 55.0), this version of the Java Runtime only recognizes class file versions up to 52.0
    // id "com.palantir.git-version" version "0.15.0"
    id "com.palantir.git-version" version "0.12.3"
}
repositories {
    mavenCentral()
}
group = "pro.civitaspo"
description = "Loads records from Dynamodb."
version = {
    def vd = versionDetails()
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^[0-9]+\.[0-9]+\.[0-9]+(\.[a-zA-Z0-9]+)?/) {
        vd.lastTag
    } else {
        "0.0.0.${vd.gitHash}"
    }
}()

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "org.scala-lang:scala-library:2.13.1"

    implementation  "org.embulk:embulk-core:0.9.23"

    implementation "com.amazonaws:aws-java-sdk-dynamodb:1.11.711"
    implementation "com.amazonaws:aws-java-sdk-sts:1.11.711"
    // For @delegate macro.
    implementation "dev.zio:zio-macros-core_2.13:0.6.2"

    testImplementation "junit:junit:4.+"
    testImplementation "org.embulk:embulk-core:0.9.23:tests"
    testImplementation "org.embulk:embulk-standards:0.9.23"
    testImplementation "org.embulk:embulk-deps-buffer:0.9.23"
    testImplementation "org.embulk:embulk-deps-config:0.9.23"
}
compileScala {
    scalaCompileOptions.additionalParameters = [
        "-Ymacro-annotations"
    ]
}
test {
    jvmArgs '-Xms4g', '-Xmx4g', '-XX:MaxMetaspaceSize=1g'
    maxHeapSize = "4g"
}
spotless {
    scala {
        scalafmt('3.7.1').configFile('.scalafmt.conf')
    }
}
embulkPlugin {
    mainClass = "org.embulk.input.dynamodb.DynamodbInputPlugin"
    category = "input"
    type = "dynamodb"
}
publishing {
    publications {
        embulkPluginMaven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url = "${project.buildDir}/mavenPublishLocal"
        }
    }
}
gem {
    from("LICENSE.txt")
    authors = ["Daisuke Higashi", "Civitaspo"]
    email = ["daisuke.develop@gmail.com", "civitaspo@gmail.com"]
    summary = "An Embulk plugin to ingest data from Dynamodb."
    homepage = "https://github.com/lulichn/embulk-input-dynamodb"
    licenses = [ "MIT" ]
}
