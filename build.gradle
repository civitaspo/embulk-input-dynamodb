plugins {
    id "com.jfrog.bintray" version "1.1"
    id "com.github.jruby-gradle.base" version "1.5.0"
    id "scala"
    id "com.diffplug.gradle.spotless" version "3.27.1"
}

import com.github.jrubygradle.JRubyExec
repositories {
    mavenCentral()
    jcenter()
}
configurations {
    provided
}

version = "0.3.2"

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile "org.scala-lang:scala-library:2.13.1"

    compile  "org.embulk:embulk-core:0.9.23"
    provided "org.embulk:embulk-core:0.9.23"

    compile "com.amazonaws:aws-java-sdk-dynamodb:1.11.711"
    compile "com.amazonaws:aws-java-sdk-sts:1.11.711"
    // For @delegate macro.
    compile "dev.zio:zio-macros-core_2.13:0.6.2"

    testCompile "junit:junit:4.+"
    testCompile "org.embulk:embulk-core:0.9.23:tests"
    testCompile "org.embulk:embulk-standards:0.9.23"
    testCompile "org.embulk:embulk-deps-buffer:0.9.23"
    testCompile "org.embulk:embulk-deps-config:0.9.23"
}

compileScala {
    scalaCompileOptions.additionalParameters = [
        "-Ymacro-annotations"
    ]
}

test {
    jvmArgs '-Xms4g', '-Xmx4g', '-XX:MaxMetaspaceSize=1g'
    maxHeapSize = "4g"
}

spotless {
    scala {
        scalafmt('2.3.2').configFile('.scalafmt.conf')
    }
}

task classpath(type: Copy, dependsOn: ["jar"]) {
    doFirst { file("classpath").deleteDir() }
    from (configurations.runtime - configurations.provided + files(jar.archivePath))
    into "classpath"
}
clean { delete "classpath" }

task gem(type: JRubyExec, dependsOn: ["gemspec", "classpath"]) {
    jrubyArgs "-S"
    script "gem"
    scriptArgs "build", "${project.name}.gemspec"
    doLast { ant.move(file: "${project.name}-${project.version}.gem", todir: "pkg") }
}

task gemPush(type: JRubyExec, dependsOn: ["gem"]) {
    jrubyArgs "-S"
    script "gem"
    scriptArgs "push", "pkg/${project.name}-${project.version}.gem"
}

task "package"(dependsOn: ["gemspec", "classpath"]) {
    doLast {
        println "> Build succeeded."
        println "> You can run embulk with '-L ${file(".").absolutePath}' argument."
    }
}

task gemspec {
    ext.gemspecFile = file("${project.name}.gemspec")
    inputs.file "build.gradle"
    outputs.file gemspecFile
    doLast { gemspecFile.write($/
Gem::Specification.new do |spec|
  spec.name          = "${project.name}"
  spec.version       = "${project.version}"
  spec.authors       = ["Daisuke Higashi", "Civitaspo"]
  spec.summary       = %[Dynamodb input plugin for Embulk]
  spec.description   = %["Loads records from Dynamodb."]
  spec.email         = ["daisuke.develop@gmail.com", "civitaspo@gmail.com"]
  spec.licenses      = ["MIT"]
  spec.homepage      = "https://github.com/lulichn/embulk-input-dynamodb"

  spec.files         = `git ls-files`.split("\n") + Dir["classpath/*.jar"]
  spec.test_files    = spec.files.grep(%r"^(test|spec)/")
  spec.require_paths = ["lib"]

  spec.add_development_dependency 'bundler', ['~> 1.0']
  spec.add_development_dependency 'rake', ['~> 12.0']
end
/$)
    }
}
clean { delete "${project.name}.gemspec" }

